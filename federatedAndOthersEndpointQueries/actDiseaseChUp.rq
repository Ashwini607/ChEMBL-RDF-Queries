# Retrieve the biological activity of associated with Alzheimer disease
# 

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dbpedia2: <http://dbpedia.org/property/>
PREFIX dbpedia: <http://dbpedia.org/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX cco: <http://rdf.ebi.ac.uk/terms/chembl#>

PREFIX up:<http://purl.uniprot.org/core/> 


SELECT DISTINCT ?protein ?act ?std_value ?std_unit
WHERE {
  ?molecule cco:hasActivity ?act.
  OPTIONAL{ ?act cco:standardValue ?std_value ;
                 cco:standardUnits ?std_unit.}
  ?act cco:hasAssay ?assay.
  ?assay cco:hasTarget ?target .
  ?target cco:hasTargetComponent ?targetcmpt .
  ?targetcmpt skos:exactMatch ?protein .
   SERVICE <http://beta.sparql.uniprot.org/sparql> {
       ?protein a up:Protein.
       ?protein up:annotation ?annotation .
       ?annotation a up:Disease_Annotation .
       ?annotation up:disease ?dis.
       ?dis skos:prefLabel ?disease.
       FILTER regex(?disease ,"Alzheimer", 'i')
}
}

