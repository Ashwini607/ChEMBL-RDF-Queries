# Retrieve compound activity details for a target such as activity standerd (value, type
# unit, relation), and description about assay.  
# Target is Human PDE5 (CHEMBL1827) 

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dbpedia2: <http://dbpedia.org/property/>
PREFIX dbpedia: <http://dbpedia.org/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX cco: <http://rdf.ebi.ac.uk/terms/chembl#>


SELECT ?ChEMBL_id ?std_type ?std_rel ?std_value ?std_unit  ?ass_disc 
WHERE {
  ?molecule cco:hasActivity ?act.
  ?molecule rdfs:label ?ChEMBL_id .
  ?act cco:hasAssay ?ass.
  OPTIONAL{?act cco:standardType ?std_type .}
  OPTIONAL{?act cco:standardRelation ?std_rel.}
  OPTIONAL{?act cco:standardValue ?std_value .}
  OPTIONAL{?act cco:standardUnits ?std_unit .}
  ?ass dcterms:description ?ass_disc.
  ?ass cco:hasTarget <http://rdf.ebi.ac.uk/resource/chembl/target/CHEMBL1827>.
}
